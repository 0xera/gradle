configurations {
    classycle
}

dependencies {
    classycle 'classycle:classycle:1.4@jar'
}

sourceSets.all { sourceSet ->
    def taskName = sourceSet.getTaskName('classycle', null)
    task(taskName){
        def reportFile = file("$reportsDir/classcycle/${sourceSet.name}.xml")
        inputs.files sourceSet.output
        outputs.file reportFile
        doLast {
            ant.taskdef(name: "classycleDependencyCheck", classname: "classycle.ant.DependencyCheckingTask", classpath: configurations.classycle.asPath)
            reportFile.parentFile.mkdirs()
            ant.classycleDependencyCheck(reportFile: reportFile, failOnUnwantedDependencies: true, mergeInnerClasses: true,
                """
                    check absenceOfPackageCycles > 1 in org.gradle.*
                    check absenceOfClassCycles > 1 in org.gradle.*
                """
            ) {
                fileset(dir: sourceSet.output.classesDir)
            }
        }
    }
    check.dependsOn taskName
}
