/*
 * Copyright 2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.util.regex.Pattern

apply plugin: "base"
evaluationDependsOn ":docs"

apply from: "${rootDir}/gradle/ssh.gradle"

tasks.withType(SshTask) {
    host = 'gradle01.managed.contegix.com'
    userName = project.hasProperty('websiteScpUserName') ? project.websiteScpUserName : null
    password = project.hasProperty('websiteScpUserPassword') ? project.websiteScpUserPassword : null
}

// fail early if we don't have credentials to connect to the host
gradle.taskGraph.whenReady { graph ->
    if (tasks.withType(SshTask).any { graph.hasTask(it) }) {
        project.websiteScpUserName
        project.websiteScpUserPassword
    }
}

uploadPath = { "/var/local/gradle-build-uploads/$it" }

// There's a script on the host that will allow running certain commands as sudo, to enable
// us to run some commands with privs. This script is configured via sudoers to allow shh exec
// from any user in the wheel group. That is, you must be a member of wheel to upload to the host.
asSudo = { Object[] args -> (["sudo", uploadPath("asSudo")] + args.toList()) as Object[] }

def dists = rootProject.configurations.dists
task uploadDistributions(type: SshTask, dependsOn: [rootProject.testedDists, dists.allArtifacts.buildDependencies]) {
    verbose true
    doFirst {
        dists.allArtifacts.each { artifact ->
            scpFile artifact.file, uploadPath("distributions/${artifact.file.name}")
        }
        ssh asSudo("move-distributions", project.version, project.version.release ? "release" : "snapshot")
    }
}

task generateReleasesXml() {
    outputFile = file("$buildDir/releases.xml")
    outputs.file outputFile
    outputs.upToDateWhen { false }

    doLast {
        project.releases.modifyTo(outputFile) {
            def releases = release

            current[0] + {
                def readVersion = { label ->
                    try {
                        def text = new URL("http://gradle.org/versions/$label").text
                        new groovy.json.JsonSlurper().parseText(text)
                    } catch (FileNotFoundException e) {
                        // service returns 404 if there is no version with that label,
                        // so we get a FileNotFoundException from URL.text
                        null
                    }
                }

                // nightly
                def nightlyVersion
                def nightlyBuildTime
                if (this.project.isNightlyBuild()) {
                    nightlyVersion = this.project.version
                    nightlyBuildTime = this.project.version.timestamp
                } else {
                    def nightlyRemote = readVersion("nightly")
                    if (nightlyRemote) {
                        nightlyVersion = nightlyRemote.version
                        nightlyBuildTime = nightlyRemote.buildTime
                    }
                }

                if (nightlyVersion && nightlyBuildTime) {
                    release(version: nightlyVersion, "build-time": nightlyBuildTime, nightly: true, snapshot: true)
                }

                // rc
                if (!this.project.isFinalReleaseBuild()) { // wipe out the rc if we are in a final release
                    if (this.project.isRcBuild()) {
                        def nextNode = next[0]
                        assert nextNode
                        release(version: this.project.version, "build-time": this.project.version.timestamp, "rc-for": nextNode.@version, snapshot: true)
                    } else {
                        def rcRemote = readVersion("release-candidate")
                        if (rcRemote) {
                            release(version: rcRemote.version, "build-time": rcRemote.buildTime, "rc-for": rcRemote.rcFor, snapshot: true)
                        }
                    }
                }

                // current
                def currentVersion
                def currentBuildTime
                if (this.project.version.release) {
                    
                    /*
                        We are doing a release build.
                        We are relying on the project.releases.incrementNextVersion() NOT being called yet.
                    */
                    currentVersion = this.project.version
                    currentBuildTime = this.project.version.timestamp
                    release(version: currentVersion, "build-time": currentBuildTime, current: true)
                } else {
                    def currentRemote = readVersion("current")
                    currentVersion = currentRemote.version

                    def currentRelease = releases.find { it.@version == currentVersion }
                    assert currentRelease : "didn't find $currentVersion in source releases.xml"
                    currentRelease.@current = true
                }
            }

            [next, current]*.each { remove(it) }
        }
    }
}

task checkoutRepo(type: Exec) {
    checkoutDir = file("$buildDir/repo-master")
    onlyIf { !checkoutDir.exists() }
    executable "git"
    args "clone", "git@github.com:gradleware/web.git", checkoutDir
}

task pushReleasesXml {
    dependsOn checkoutRepo, generateReleasesXml
    outputs.upToDateWhen { false }
    repo = project.file("$project.buildDir/repo-$name")

    doLast {
        def masterCheckout = project.checkoutRepo.checkoutDir
        def gitOnMasterCheckout = { Object[] cliArgs ->
            project.exec {
                workingDir masterCheckout
                executable "git"
                args cliArgs
            }
        }

        gitOnMasterCheckout "reset", "--hard", "HEAD"
        gitOnMasterCheckout "clean", "-f", "-d"
        gitOnMasterCheckout "pull"

        project.delete repo

        // Gradle copy chokes on the symlinks
        ant.copy(todir: repo) {
            fileset(dir: masterCheckout, defaultexcludes: false)
        }

        def releasesXml = new File(repo, "provided/release-data/releases.xml")
        def checkedInReleasesXmlText = releasesXml.text
        def newReleasesXmlText = generateReleasesXml.outputFile.text
        if (checkedInReleasesXmlText != newReleasesXmlText) {
            releasesXml.text = newReleasesXmlText

            def gitOnTaskRepo = { Object[] cliArgs ->
                project.exec {
                    workingDir repo
                    executable "git"
                    args cliArgs
                }
            }

            def message = "updating releases.xml from "
            if (isFinalReleaseBuild()) {
                message += "final release build"
            } else if (isNightlyBuild()) {
                message += "nightly build"
            } else if (isRcBuild()) {
                message += "release-candidate build"
            } else {
                message += "adhoc build"
            }

            gitOnTaskRepo "add", releasesXml.absolutePath
            gitOnTaskRepo "commit", "-m", "[gradle-build] $message"

            if (!project.hasProperty("noPushReleasesXml")) {
                gitOnTaskRepo "push"
            }
        } else {
            println "Not pushing new releases.xml to site as there were no changes after generation"
        }
    }

}


task docsWithAnalytics(type: PatternTransform) {
    inputs.files project(":docs").docsZip
    from { zipTree(project(":docs").docsZip.outputs.files.singleFile) }
    into "$buildDir/$name"
    transform ".*(?<!(javadoc|groovydoc)/index)\\.html", Pattern.compile("<\\s*/\\s*head\\s*>", Pattern.CASE_INSENSITIVE), """
            <script type="text/javascript">
              var _gaq = _gaq || [];
              _gaq.push(['_setAccount', 'UA-4207603-1']);
              _gaq.push(['_trackPageview']);

              (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
              })();
            </script>
            <script type="text/javascript" language="javascript">llactid=20600</script>
            <script type="text/javascript" language="javascript" src="http://t3.trackalyzer.com/trackalyze.js"></script>
        </head>
    """.trim()
}

task docsWithAnalyticsZip(type: Zip) {
    from docsWithAnalytics
}

task uploadDocs(type: Scp) {
    verbose true
    isDir false
    source docsWithAnalyticsZip
    destination = uploadPath("docs/${->project.version.label}.zip")

    doLast {
        // unpack the zip to the right place and fix the perms
        ssh asSudo("move-docs", project.version.label)

        if (project.version.release) {
            // update the “current” symlink in the docs dir
            ssh asSudo("mark-current-docs", project.version)
        }
    }
}

class PatternTransform extends Sync {

    private transformCounter = 0

    void transform(relativePathPattern, toReplacePattern, replaceWith) {
        relativePathPattern = compilePattern(relativePathPattern)
        toReplacePattern = compilePattern(toReplacePattern)

        inputs.property "transform:${transformCounter++}", [
            relativePathPattern: asMap(relativePathPattern),
            toReplacePattern: asMap(toReplacePattern),
            replaceWith: replaceWith
        ]

        eachFile {
            if (relativePathPattern.matcher(it.relativePath.toString()).matches()) {
                it.filter {
                    toReplacePattern.matcher(it).replaceAll(replaceWith)
                }
            }
        }
    }

    private asMap(Pattern pattern) {
        [pattern: pattern.toString(), flags: pattern.flags]
    }

    protected Pattern compilePattern(pattern) {
        pattern instanceof Pattern ? pattern : Pattern.compile(pattern)
    }
}
