// Copyright 2017 the original author or authors.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

[[working_with_files]]
== Working With Files

Almost every Gradle build works with files: think source files, library dependencies, reports and so on. That's why Gradle comes with a comprehensive API that makes it simple to achieve common tasks.

The API has two parts to it:

 * Specifying which files and directories to process
 * Specifying what to do with them

We look at these parts in more detail later in the chapter, but we'll start with how you accomplish some of the most common tasks users need.

=== Copying a single file

You copy a file by creating an instance of Gradle's builtin api:org.gradle.api.tasks.Copy[] task and configuring it with the location of the file and where you want to put it. This example mimics copying a generated report into a directory that will be packed into an archive (zip, tarball, or whatever):

----
task copyReport(type: Copy) {
    from file("${buildDir}/reports/my-report.pdf")
    into file("${buildDir}/toArchive")
}
----

The api:org.gradle.api.Project#file(java.lang.Object)[file()] method is used to create a file or directory path relative to the current project and is a common way to locate files in the project directory.

You can even use the path directly without the `file()` method:

----
task copyReport(type: Copy) {
    from "${buildDir}/reports/my-report.pdf"
    into "${buildDir}/toArchive"
}
----

TODO: Link this to a later section that explains how this implicit file path works

Note that hardcoding paths like this makes the build brittle. It's better to use the task that generates a file as the source of the file's location. In this modified example, we use a report task defined elsewhere that has the report's location stored in the `outputFile` property:

----
ext.archiveDir = file("${buildDir}/toArchive")

task copyReport(type: Copy) {
    from myReportTask.outputFile
    into archiveDir
}
----

We also moved the hardcoded archive directory into a project property, which only needs to be changed in one place to apply to the whole build.

TODO: Link to more detailed coverage of `from()` arguments

=== Copying multiple files

You can extend the previous examples to multiple files very easily by providing multiple arguments to `from()`:

----
task copyReports(type: Copy) {
    from "${buildDir}/reports/my-report.pdf", "src/docs/manual.pdf"
    into "${buildDir}/toArchive"
}
----

Now two files are copied into the archive directory.

What if you want to copy all the PDFs in a directory without having to specify each one? You can easily attach both inclusion and exclusion patterns to the copy specification. Here we use a pattern to include only PDFs:

----
task copyReports(type: Copy) {
    from "${buildDir}/reports"
    include "*.pdf"
    into "${buildDir}/toArchive"
}
----

One thing to note in this example is that only PDFs directly in the `reports` directory are copied. If there are PDFs in subdirectories of `reports`, they'll be left out. You can change that by using an Ant-style glob pattern:

----
task copyReports(type: Copy) {
    from "${buildDir}/reports"
    include "**/*.pdf"
    into "${buildDir}/toArchive"
}
----

This approach has the side effect of copying the directory structure below `reports` as well as the files. This may be what you want. It may not be. Copying files includes many such decisions and there are too many to cover here, but know that there are very few requirements that can't be handled elegantly by Gradle copy specifications.

Head over to ~TODO~ in order to learn all the different options available for getting the exact behavior you want.

=== Copying directory hierarchies

You may have the need to copy not just files, but the directory structure they reside in as well. This is, in fact, the default behavior when you specify a directory as the `from()` argument. So the following will copy everything in the `reports` directory, including all its subdirectories:

----
task copyReports(type: Copy) {
    from "${buildDir}/reports"
    into "${buildDir}/toArchive"
}
----

The key aspect that users struggle with is controlling how much of the directory structure goes to the destination. In the above example, do you get a `toArchive/reports` directory or does everything in `reports` go straight into `toArchive`? The answer is the latter. If a directory is part of the `from()` path, then it *won't* appear in the destination.

So how do you ensure that `reports` itself is copied across, but not any other directory in `$buildDir`? The answer is to add it as an include pattern:

----
task copyReports(type: Copy) {
    from("${buildDir}") {
        include "reports/**"
    }
    into "${buildDir}/toArchive"
}
----

You'll get the same behavior as before except with one extra level of directory in the destination, i.e. `toArchive/reports`.

One thing to note is how the `include()` directive applies only to the `from()`, whereas the directive in the previous section applied to the whole task. These different levels of granularity in the copy specification allow you to easily handle most requirements that you will come across. TODO: Link to more details on copy specs.

=== Creating archives (zip, tar, etc.)

From the perspective of Gradle, packing files into a single archive is effectively a copy in which the destination is an archive file instead of a directory on the file system. This means that archiving files looks a lot like copying, with all of the same features!

The simplest case involves packing all the contents of a directory into an archive, such as a zip in this example:

----
task packageDistribution(type: Zip) {
    archiveName = "my-distribution.zip"
    destinationDir = file("${buildDir}/dist")

    from "${buildDir}/toArchive"
}
----

Each type of archive has its own task type, the most common ones being api:org.gradle.api.tasks.bundling.Zip[], api:org.gradle.api.tasks.bundling.Tar[] and api:org.gradle.api.tasks.bundling.Jar[]. And instead of having an `into()` setting like `Copy`, you configure the location and name of the destination archive file. In many cases you don't even need to do that as `destinationDir` defaults to `${buildDir}/distributions` and many plugins provide conventions for `archiveName`.

The archiving tasks share most of the configuration options of `Copy`, including filtering and renaming. One of the most common options people use allows you to create a subdirectory in the destination to copy the files into. For example, let's say you want to package all PDFs into a `docs` directory in the root of the archive. This `docs` directory doesn't exist in the source location, so you have to create it as part of the archive. You do this by adding an `into()` declaration for just the PDFs:

----
task packageDistribution(type: Zip) {
    archiveName = "my-distribution.zip"
    destinationDir = file("${buildDir}/dist")

    from("${buildDir}/toArchive") {
        exclude "**/*.pdf"
    }

    from("${buildDir}/toArchive") {
        include "**/*.pdf"
        into "docs"
    }
}
----

As you can see, you can have multiple `from()` declarations in a copy specification, each with its own configuration.

[[sec:unpacking_archives_example]]
=== Unpacking archives

Archives are effectively self-contained file systems, so unpacking them is a case of copying the files from that file system into a directory (or even another archive). Gradle enables this by providing some wrapper functions that make archives available as hierarchical collections of files known as file trees (TODO: link to an in-depth discussion of file trees).

The two functions of interest are api:org.gradle.api.Project#zipTree[] and api:org.gradle.api.Project#tarTree[], which produce a api:org.gradle.api.file.FileTree[] from a corresponding archive file. That file tree can then be used in a `from()` specification, like so:

----
task unpackFiles(type: Copy) {
    from zipTree("src/resources/thirdPartyResources.zip")
    into "${buildDir}/resources"
}
----

As with a normal copy, you can control which files are unpacked via filters and even rename files as they are unpacked.

=== Creating directories

Many tasks need to create directories to store the files they generate, which is why Gradle automatically manages this aspect of tasks when they explicitly define file and directory outputs. You can learn about this feature in the <<sec:up_to_date_checks,incremental build>> section of the user guide. All core Gradle tasks ensure that any output directories they need are created if necessary using this mechanism.

In cases where you need to create a directory manually, you can use the api:org.gradle.api.Project#mkdir[] method from within your build scripts or custom task implementations. Here's a simple example:

----
task ensureDirectory {
    doLast {
        mkdir "images"
    }
}
----

=== Moving files and directories

Gradle has no API for moving files and directories around, but you can use the <<sec:ant,Apache Ant integration>> to easily do that, as shown in this example:

----
task moveReports {
    doLast {
        ant.move file: "${buildDir}/reports", todir: "${buildDir}/toArchive"
    }
}
----

This is not a common requirement and should be used sparingly as you lose information and can easily break a build. It's generally preferable to copy directories and files instead.

=== Renaming files on copy

The files used and generated by your builds sometimes don't have names that suit, for whatever reason, so when you copy them you want to modify those names. Gradle allows you to do this as part of a copy specification using the `rename()` configuration.

The following example removes the "-staging-" marker from the names of any files that include it in a Java web application:

----
task rename(type: Copy) {
    from "src/main/webapp"
    into "${buildDir}/explodedWar"

    rename '(.+)-staging-(.+)', '$1$2'
}
----

You can use regular expressions for this, as in the above example, or closures that use more complex logic to determine the target filename. For example, the following task truncates filenames:

----
task copyWithTruncate(type: Copy) {
    from "${buildDir}/reports"
    rename { String filename ->
        if (filename.size() > 10) {
            return filename[0..7] + "~" + filename.size()
        }
        else return filename
    }
    into "${buildDir}/toArchive"
}
----

As with filtering, you can also apply renaming to a subset of files to copy by configuring it on a `from()` instead of at the task level.

[[sec:locating_files]]
=== File paths in depth

In order to perform some action on a file, you need to know where it is, and that's the information provided by file paths. Gradle builds on the standard Java `java.io.File` class, which represents the location of a single file, and provides new APIs for dealing with collections of paths. This section shows you how to use the Gradle APIs to specify file paths that tasks can use.

[[sec:single_file_paths]]
==== Single file paths

One of the great quandaries when developing a build is how to specify file locations when the build may be executed from an arbitrary directory — not necessarily in the project — and may be run on any number of different systems with incompatible directory layouts. The standard Java mechanism for specifying a file path runs into trouble in these situations:

 * `new File(relative path)` generates a path relative to the current working directory, which could be anywhere
 * `new File(absolute path)` will fail if the file system doesn't have the requisite path.

Gradle solves this problem by providing the api:org.gradle.api.Project#file(java.lang.Object)[] method, which generates a path relative to the _project_ directory (unless the given path is absolute, in which case it is used as is). Here are some examples of using the `file()` method with different types of argument:

++++
<sample id="resolveFile" dir="userguide/files/file" title="Locating files">
    <sourcefile file="build.gradle" snippet="simple-params"/>
</sample>
++++

As you can see, you can pass strings, `File` instances and `Path` instances to the `file()` method, all of which result in an absolute `File` object. You can find other options for argument types in the reference guide, linked in the previous paragraph.

What happens in the case of multi-project builds? The `file()` method will always turn relative paths into paths that are relative to the current project directory, which may be a child project. If you want to use a path that's relative to the _root project_ directory, then you need to use the special api:org.gradle.api.Project#getRootDir()[] property to construct an absolute path, like so:

----
File configFile = file("${rootDir}/shared/config.xml")
----

Let's say you're working on a multi-project build in a `dev/projects/AcmeHealth` directory. You use the above example in the build of the library you're fixing — at `AcmeHealth/subprojects/AcmePatientRecordLib/build.gradle`. The file path will resolve to the absolute version of `dev/projects/AcmeHealth/shared/config.xml`.

The `file()` method can be used to configure any task that has a property of type `File`. Many tasks, though, work on multiple files, so we look at how to specify sets of files next.

[[sec:file_collections]]
==== File collections

A _file collection_ is simply a set of file paths that's represented by the api:org.gradle.api.file.FileCollection[] interface. _Any_ file paths. It's important to understand that the file paths don't have to be related in any way, so they don't have to be in the same directory or even have a shared parent directory. You will also find that many parts of the Gradle API use `FileCollection`, such as the copying API discussed later in this chapter and <<sub:configurations,dependency configurations>>.

The recommended way to specify a collection of files is to use the api:org.gradle.api.Project#files(java.lang.Object...)[] method, which returns a `FileCollection` instance. This method is very flexible and allows you to pass multiple strings, `File` instances, collections of strings, collections of `File`s, and more. You can even pass in tasks if they have <<sec:task_inputs_outputs,defined outputs>>. Learn about all the supported argument types in the reference guide.

As with the api:org.gradle.api.Project#file(java.lang.Object)[] method covered in the <<sec:single_file_paths,previous section>>, all relative paths are evaluated relative to the current project directory. The following example demonstrates some of the variety of argument types you can use, using strings, `File` instances, a list and a `Path`:

++++
<sample id="fileCollections" dir="userguide/files/fileCollections" title="Creating a file collection">
    <sourcefile file="build.gradle" snippet="simple-params"/>
</sample>
++++

File collections have some important attributes in Gradle. They can be:

 * created lazily
 * iterated over
 * filtered
 * combined

_Lazy creation_ of a file collection is useful when you need to evaluate the files that make up a collection when the build runs. In the following example, we query the file system to find out what files exist in a particular directory and make them a file collection:

++++
<sample id="fileCollections" dir="userguide/files/fileCollections" title="Implementing a file collection">
    <sourcefile file="build.gradle" snippet="closure"/>
    <output args="-q list"/>
</sample>
++++

The key to lazy creation is passing a closure to the `files()` method. Your closure simply needs to return a value of a type accepted by `files()`, such as `List<File>`, `String`, `FileCollection`, etc.

_Iterating over a file collection_ can be done through the `each()` method on the collection or using the collection in a `for` loop. In both approaches, the file collection is treated as a set of `File` instances, i.e. your iteration variable will be of type `File`.

The following example demonstrates such iteration as well as how you can convert file collections to other types using the `as` operator or supported properties:

++++
<sample id="fileCollections" dir="userguide/files/fileCollections" title="Using a file collection">
    <sourcefile file="build.gradle" snippet="usage"/>
    <test args="-q usage"/>
</sample>
++++

You can also see at the end of the example _how to combine file collections_ using the `+` and `-` operators to merge and subtract them. An important feature of the resulting file collections is that they are _live_. In other words, when you combine file collections in this way, the result always reflects what's in the original file collections, even if they change during the build.

For example, imagine `collection` in the above example gains an extra file or two after `union` is created. As long as you use `union` after those files are added to `collection`, `union` will also contain those additional files. The same goes for the `different` file collection.

Live collections are also important when it comes to _filtering_. If you want to use a subset of a file collection, you can take advantage of the api:org.gradle.api.file.FileCollection#filter(org.gradle.api.specs.Spec)[] method to determine which files to "keep". In the following example, we create a new collection that consists of only the files that end with .txt in the source collection:

----
FileCollection textFiles = collection.filter { File f ->
    f.name.endsWith(".txt")
}
----

If `collection` changes at any time, either by adding or removing files from itself, then `textFiles` will immediately reflect the change because it is also a live collection. Note that the closure you pass to `filter()` takes a `File` as an argument and should return a boolean.

[[sec:file_trees]]
==== File trees

A _file tree_ is a file collection that retains the directory structure of the files it contains and has the type api:org.gradle.api.file.FileTree[]. This means that all the paths in a file tree must have a shared parent directory. The following diagram highlights the distinction between file trees and file collections in the common case of copying files:

++++
<figure>
    <title>The differences in how file trees and file collections behave when copying files</title>
    <imageobject>
        <imagedata fileref="img/file-collection-vs-file-tree.png" width="160mm"/>
    </imageobject>
</figure>
++++

NOTE: Although `FileTree` extends `FileCollection` (an is-a relationship), their behaviors do differ. In other words, you can use a file tree wherever a file collection is required, but remember: a file collection is a flat list/set of files, while a file tree is a file and directory hierarchy. To convert a file tree to a flat collection, use the api:org.gradle.api.file.FileTree#getFiles()[] property.

The simplest way to create a file tree is to pass a file or directory path to the api:org.gradle.api.Project#fileTree(java.lang.Object)[] method. This will create a tree of all the files and directories in that base directory (but not the base directory itself). The following example demonstrates how to use the basic method and, in addition, how to filter the files and directories using Ant-style patterns:

++++
<sample id="fileTrees" dir="userguide/files/fileTrees" title="Creating a file tree">
    <sourcefile file="build.gradle" snippet="define"/>
</sample>
++++

You can see more examples of supported patterns in the API docs for api:org.gradle.api.tasks.util.PatternFilterable[]. Also, see the API documentation for `fileTree()` to see what types you can pass as the base directory.

[NOTE]
====
By default, the `FileTree` instance `fileTree()` returns will apply some Ant-style default exclude patterns for convenience. For the complete default exclusion list, see http://ant.apache.org/manual/dirtasks.html#defaultexcludes[Default Excludes].
====

You can do many of the same things with file trees that you can with file collections:

 * iterate over them (depth first)
 * filter them (using api:org.gradle.api.file.FileTree#matching(org.gradle.api.Action)[] and Ant-style patterns)
 * merge them

You can also traverse file trees using the api:org.gradle.api.file.FileTree#visit(org.gradle.api.Action)[] method. All of these techniques are demonstrated in the following example: 

++++
<sample id="fileTrees" dir="userguide/files/fileTrees" title="Using a file tree">
    <sourcefile file="build.gradle" snippet="use"/>
</sample>
++++

We've discussed how to create your own file trees and file collections, but it's also worth bearing in mind that many Gradle plugins provide their own instances of file trees, such as <<sec:java_source_sets,Java's source sets>>. These can be used and manipulated in exactly the same way as the file trees you create yourself.

Another specific type of file tree that users commonly need is the archive, i.e. ZIP files, TAR files, etc. We look at those next.

[[sec:archive_contents]]
==== Using archives as file trees

An archive is a directory and file hierarchy packed into a single file. In other words, it's a special case of a file tree, and that's exactly how Gradle treats archives. Instead of using the `fileTree()` method, which only works on normal file systems, you use the api:org.gradle.api.Project#zipTree[] and api:org.gradle.api.Project#tarTree(java.lang.Object)[] methods to wrap archive files of the corresponding type (note that JAR, WAR and EAR files are ZIPs). Both methods return `FileTree` instances that you can then use in the same way as normal file trees. For example, you can extract some or all of the files of an archive by copying its contents to some directory on the file system. Or you can merge one archive into another.

Here are some simple examples of creating archive-based file trees:

++++
<sample id="fileTrees" dir="userguide/files/fileTrees" title="Using an archive as a file tree">
    <sourcefile file="build.gradle" snippet="archive-trees"/>
</sample>
++++

You can see a practical example of extracting an archive file <<sec:unpacking_archives_example,earlier in the chapter>>.

[[sec:specifying_multiple_files]]
==== Understanding implicit conversion to file collections

TODO: Review this section for accuracy

Many objects in Gradle have properties which accept a set of input files. For example, the api:org.gradle.api.tasks.compile.JavaCompile[] task has a `source` property, which defines the source files to compile. You can set the value of this property using any of the types supported by the <<sec:file_collections,files()>> method, which was shown above. This means you can set the property using, for example, a `File`, `String`, collection, `FileCollection` or even a closure. Here are some examples:

++++
<sample id="inputFiles" dir="userguide/files/inputFiles" title="Specifying a set of files">
    <sourcefile file="build.gradle" snippet="set-input-files"/>
</sample>
++++

Usually, there is a method with the same name as the property, which appends to the set of files. Again, this method accepts any of the types supported by the <<sec:file_collections,files()>> method.

++++
<sample id="inputFiles" dir="userguide/files/inputFiles" title="Appending a set of files">
    <sourcefile file="build.gradle" snippet="add-input-files"/>
</sample>
++++


[[sec:copying_files]]
=== Copying files

You can use the api:org.gradle.api.tasks.Copy[] task to copy files. The copy task is very flexible, and allows you to, for example, filter the contents of the files as they are copied, and map to the file names.

To use the `Copy` task, you must provide a set of source files to copy, and a destination directory to copy the files to. You may also specify how to transform the files as they are copied. You do all this using a _copy spec_. A copy spec is represented by the api:org.gradle.api.file.CopySpec[] interface. The `Copy` task implements this interface. You specify the source files using the api:org.gradle.api.file.CopySpec#from(java.lang.Object...)[] method. To specify the destination directory, use the api:org.gradle.api.file.CopySpec#into(java.lang.Object)[] method.

++++
<sample id="copy" dir="userguide/files/copy" title="Copying files using the copy task">
    <sourcefile file="build.gradle" snippet="copy-task"/>
    <test args="test"/>
</sample>
++++

The `from()` method accepts any of the arguments that the <<sec:file_collections,files()>> method does. When an argument resolves to a directory, everything under that directory (but not the directory itself) is recursively copied into the destination directory. When an argument resolves to a file, that file is copied into the destination directory. When an argument resolves to a non-existing file, that argument is ignored. If the argument is a task, the output files (i.e. the files the task creates) of the task are copied and the task is automatically added as a dependency of the `Copy` task. The `into()` accepts any of the arguments that the <<sec:locating_files,file()>> method does. Here is another example:

++++
<sample id="copy" dir="userguide/files/copy" title="Specifying copy task source files and destination directory">
    <sourcefile file="build.gradle" snippet="copy-task-2"/>
</sample>
++++

You can select the files to copy using Ant-style include or exclude patterns, or using a closure:

++++
<sample id="copy" dir="userguide/files/copy" title="Selecting the files to copy">
    <sourcefile file="build.gradle" snippet="copy-task-with-patterns"/>
</sample>
++++

You can also use the api:org.gradle.api.Project#copy(org.gradle.api.Action)[] method to copy files. It works the same way as the task with some major limitations though. First, the `copy()` is not incremental (see <<sec:up_to_date_checks>>).

++++
<sample id="copy" dir="userguide/files/copy" title="Copying files using the copy() method without up-to-date check">
    <sourcefile file="build.gradle" snippet="copy-method"/>
</sample>
++++

Secondly, the `copy()` method cannot honor task dependencies when a task is used as a copy source (i.e. as an argument to `from()`) because it's a method and not a task. As such, if you are using the `copy()` method as part of a task action, you must explicitly declare all inputs and outputs in order to get the correct behavior.

++++
<sample id="copy" dir="userguide/files/copy" title="Copying files using the copy() method with up-to-date check">
    <sourcefile file="build.gradle" snippet="copy-method-with-dependency"/>
</sample>
++++

It is preferable to use the `Copy` task wherever possible, as it supports incremental building and task dependency inference without any extra effort on your part. The `copy()` method can be used to copy files as _part_ of a task's implementation. That is, the copy method is intended to be used by custom tasks (see <<custom_tasks>>) that need to copy files as part of their function. In such a scenario, the custom task should sufficiently declare the inputs/outputs relevant to the copy action.

[[sec:renaming_files]]
==== Renaming files

++++
<sample id="renameOnCopy" dir="userguide/files/copy" title="Renaming files as they are copied">
    <sourcefile file="build.gradle" snippet="rename-files"/>
</sample>
++++

[[sec:filtering_files]]
==== Filtering files

++++
<sample id="filterOnCopy" dir="userguide/files/copy" title="Filtering files as they are copied">
    <sourcefile file="build.gradle" snippet="filter-files"/>
</sample>
++++

When you use the `ReplaceTokens` class with the “filter” operation, the result is a template engine that replaces tokens of the form “@tokenName@” (the Apache Ant-style token) with a set of given values. The “expand” operation does the same thing except it treats the source files as http://docs.groovy-lang.org/latest/html/api/groovy/text/SimpleTemplateEngine.html[Groovy templates] in which tokens take the form “${tokenName}”. Be aware that you may need to escape parts of your source files when using this option, for example if it contains literal “$” or “&lt;%” strings.

It's a good practice to specify the charset when reading and writing the file, using the `filteringCharset` property. If not specified, the JVM default charset is used, which might not match with the actual charset of the files to filter, and might be different from one machine to another.

[[sec:using_the_copyspec_class]]
==== Using the `CopySpec` class

Copy specs form a hierarchy. A copy spec inherits its destination path, include patterns, exclude patterns, copy actions, name mappings and filters.

++++
<sample id="nestedCopySpecs" dir="userguide/files/copy" title="Nested copy specs">
    <sourcefile file="build.gradle" snippet="nested-specs"/>
</sample>
++++

[[sec:sync_task]]
=== Using the `Sync` task

The api:org.gradle.api.tasks.Sync[] task extends the `Copy` task. When it executes, it copies the source files into the destination directory, and then removes any files from the destination directory which it did not copy. This can be useful for doing things such as installing your application, creating an exploded copy of your archives, or maintaining a copy of the project's dependencies.

Here is an example which maintains a copy of the project's runtime dependencies in the `build/libs` directory.

++++
<sample id="syncDependencies" dir="userguide/files/sync" title="Using the Sync task to copy dependencies">
    <sourcefile file="build.gradle" snippet="copy-dependencies"/>
    <test args="libs"/>
</sample>
++++

[[sec:archives]]
=== Creating archives

A project can have as many JAR archives as you want. You can also add WAR, ZIP and TAR archives to your project. Archives are created using the various archive tasks: api:org.gradle.api.tasks.bundling.Zip[], api:org.gradle.api.tasks.bundling.Tar[], api:org.gradle.api.tasks.bundling.Jar[], api:org.gradle.api.tasks.bundling.War[], and api:org.gradle.plugins.ear.Ear[]. They all work the same way, so let's look at how you create a ZIP file.

++++
<sample id="createZip" dir="userguide/files/archives" title="Creating a ZIP archive">
    <sourcefile file="build.gradle" snippet="zip"/>
</sample>
++++

[TIP]
.Why are you using the Java plugin?
====
The Java plugin adds a number of default values for the archive tasks. You can use the archive tasks without using the Java plugin, if you like. You will need to provide values for some additional properties.
====

The archive tasks all work exactly the same way as the `Copy` task, and implement the same `CopySpec` interface. As with the `Copy` task, you specify the input files using the `from()` method, and can optionally specify where they end up in the archive using the `into()` method. You can filter the contents of file, rename files, and all the other things you can do with a copy spec.

[[sec:archive_naming]]
==== Archive naming

The format of `__projectName__-__version__.__type__` is used for generated archive file names. For example:

++++
<sample id="archiveNaming" dir="userguide/files/archiveNaming" title="Creation of ZIP archive">
    <sourcefile file="build.gradle"/>
    <output args="-q myZip"/>
</sample>
++++

This adds a `Zip` archive task with the name `myZip` which produces ZIP file `zipProject-1.0.zip`. It is important to distinguish between the name of the archive task and the name of the archive generated by the archive task. The default name for archives can be changed with the `archivesBaseName` project property. The name of the archive can also be changed at any time later on.

There are a number of properties which you can set on an archive task. These are listed below in <<archiveTasksNamingProperties>>. You can, for example, change the name of the archive:

++++
<sample id="zipWithCustomName" dir="userguide/tutorial/zipWithCustomName" title="Configuration of archive task - custom archive name">
    <sourcefile file="build.gradle"/>
    <output args="-q myZip"/>
</sample>
++++

You can further customize the archive names:

++++
<sample id="zipWithArguments" dir="userguide/tutorial/zipWithArguments" title="Configuration of archive task - appendix &amp; classifier">
    <sourcefile file="build.gradle"/>
    <output args="-q myZip"/>
</sample>
++++

[[archiveTasksNamingProperties]]
.Archive tasks - naming properties
[cols="a,a,a,a", options="header"]
|===
| Property name
| Type
| Default value
| Description

| `archiveName`
| `String`
| `__baseName__-__appendix__-__version__-__classifier__.__extension__`

If any of these properties is empty the trailing `-` is not added to the name.
| The base file name of the generated archive

| `archivePath`
| `File`
| `__destinationDir__/__archiveName__`
| The absolute path of the generated archive.

| `destinationDir`
| `File`
| Depends on the archive type. JARs and WARs go into `__project.buildDir__/libraries`. ZIPs and TARs go into `__project.buildDir__/distributions`.
| The directory to generate the archive into

| `baseName`
| `String`
| `__project.name__`
| The base name portion of the archive file name.

| `appendix`
| `String`
| `null`
| The appendix portion of the archive file name.

| `version`
| `String`
| `__project.version__`
| The version portion of the archive file name.

| `classifier`
| `String`
| `null`
| The classifier portion of the archive file name,

| `extension`
| `String`
| Depends on the archive type, and for TAR files, the compression type as well: `zip`, `jar`, `war`, `tar`, `tgz` or `tbz2`.
| The extension of the archive file name.
|===

[[sec:sharing_content_between_multiple_archives]]
==== Sharing content between multiple archives

You can use the api:org.gradle.api.Project#copySpec(org.gradle.api.Action)[] method to share content between archives.

[[sec:reproducible_archives]]
==== Reproducible archives

Sometimes it can be desirable to recreate archives in a byte for byte way on different machines. You want to be sure that building an artifact from source code produces the same result, byte for byte, no matter when and where it is built. This is necessary for projects like https://reproducible-builds.org/[reproducible-builds.org].

Reproducing the same archive byte for byte poses some challenges since the order of the files in an archive is influenced by the underlying file system. Each time a zip, tar, jar, war or ear is built from source, the order of the files inside the archive may change. Files that only have a different timestamp also causes archives to be slightly different between builds. All api:org.gradle.api.tasks.bundling.AbstractArchiveTask[] (e.g. Jar, Zip) tasks shipped with Gradle include <<feature_lifecycle,incubating>> support producing reproducible archives.

For example, to make a `Zip` task reproducible you need to set api:org.gradle.api.tasks.bundling.Zip#isReproducibleFileOrder()[] to `true` and api:org.gradle.api.tasks.bundling.Zip#isPreserveFileTimestamps()[] to `false`. In order to make all archive tasks in your build reproducible, consider adding the following configuration to your build file:

++++
<sample id="createZip" dir="userguide/files/archives" title="Activating reproducible archives">
    <sourcefile file="build.gradle" snippet="reproducible"/>
</sample>
++++

Often you will want to publish an archive, so that it is usable from another project. This process is described in <<artifact_management>>

[[sec:properties_files]]
=== Properties files

Properties files are used in many places during Java development. Gradle makes it easy to create properties files as a normal part of the build. You can use the api:org.gradle.api.tasks.WriteProperties[] task to create properties files.

The `WriteProperties` task also fixes a well-known problem with `Properties.store()` that can reduce the usefulness of incremental builds (see <<sec:up_to_date_checks>>). The standard Java way to write a properties file produces a unique file every time, even when the same properties and values are used, because it includes a timestamp in the comments. Gradle's `WriteProperties` task generates exactly the same output byte-for-byte if none of the properties have changed. This is achieved by a few tweaks to how a properties file is generated:

* no timestamp comment is added to the output
* the line separator is system independent, but can be configured explicitly (it defaults to `'\n'`)
* the properties are sorted alphabetically
