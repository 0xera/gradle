// tag::apply[]
// tag::publish[]
plugins {
    id 'groovy-gradle-plugin'
// end::apply[]
    id 'maven-publish'
    id 'java'
// tag::apply[]
}
// end::apply[]

group = 'com.myorg.conventions'
version = '1.0'

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = layout.buildDirectory.dir("repo")
        }
    }
}

tasks.named('publish') {
    dependsOn('check')
}

// end::publish[]

testing {
    suites {
        test {
            useJUnitPlatform()

            dependencies {
                implementation 'org.spockframework:spock-core:2.0-groovy-3.0'
            }
        }
    }
}

// tag::repositories-and-dependencies[]
repositories {
    gradlePluginPortal() // so that external plugins can be resolved in dependencies section
}

dependencies {
    implementation 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.2'
}
// end::repositories-and-dependencies[]
