plugins {
    id 'java-library'
    id 'maven-publish'
}

version = '1.0.2'
group = 'com.example'

// tag::publication[]
publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'mySecure'
            url = uri(com.example.MavenRepositoryStub.address())
        }
    }
}
// end::publication[]

// tag::credentials[]
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it.name == 'publishLibraryPublicationToMySecureRepository' }) {
        def MAVEN_USERNAME_PROPERTY = 'mavenUser'
        def MAVEN_PASSWORD_PROPERTY = 'mavenPassword'
        def mavenUser = providers.gradleProperty(MAVEN_USERNAME_PROPERTY)
        def mavenPassword = providers.gradleProperty(MAVEN_PASSWORD_PROPERTY)
        if (!mavenUser.present || !mavenPassword.present) {
            throw new GradleException("Publishing requires '$MAVEN_USERNAME_PROPERTY' and '$MAVEN_PASSWORD_PROPERTY' properties")
        }
        publishing.repositories.named('mySecure') {
            credentials {
                username = mavenUser.get()
                password = mavenPassword.get()
            }
        }
    }
}
// end::credentials[]

// the following block starts the stubbed Maven repository just before publishing
// and stops it afterwards
tasks.named('publishLibraryPublicationToMySecureRepository') {
    doFirst {
        com.example.MavenRepositoryStub.start()
    }
    doLast {
        com.example.MavenRepositoryStub.stop()
    }
}
