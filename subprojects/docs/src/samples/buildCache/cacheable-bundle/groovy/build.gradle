plugins {
    id 'java'
}

// tag::bundle-task[]
task bundle(type: JavaExec) {
    File scripts = file("scripts")
    File bundle = file("$buildDir/bundle.js")

    outputs.cacheIf { true }

    inputs.dir(scripts)
        .withPropertyName("scripts")
        .withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.file(bundle)
        .withPropertyName("bundle")

    main = "org.gradle.sample.Bundle"
    classpath = project.sourceSets.main.runtimeClasspath
    args projectDir.toPath().relativize(scripts.toPath()).toString(), projectDir.toPath().relativize(bundle.toPath()).toString()
}
// end::bundle-task[]

task printBundle {
    dependsOn bundle
    doLast {
        println file("$buildDir/bundle.js").text
    }
}
