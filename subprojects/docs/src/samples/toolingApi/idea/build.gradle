apply plugin: 'java'
apply plugin: 'application'

def toolingApiVersion = gradle.gradleVersion

repositories {
    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }
    mavenCentral()
}

dependencies {
    compile "org.gradle:gradle-tooling-api:${toolingApiVersion}"
    // Need an SLF4J implementation at runtime
    runtime 'org.slf4j:slf4j-simple:1.6.6'
}

mainClassName = 'org.gradle.sample.Main'

run {
    ["org.gradle.daemon.idletimeout", "org.gradle.daemon.registry.base"].each {
        def value = gradle.startParameter.mergedSystemProperties[it]
        if (value) {
            systemProperty it, value
        }
    }
    systemProperty "gradle.user.home", gradle.startParameter.gradleUserHomeDir
}
