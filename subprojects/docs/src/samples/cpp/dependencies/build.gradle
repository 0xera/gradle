import org.gradle.api.internal.artifacts.publish.DefaultPublishArtifact

project(":lib") {
    apply plugin: "cpp-lib"
    apply plugin: 'maven'

    version = 1.0

    def libArtifact = new DefaultPublishArtifact(
        "lib", // name
        "so", // ext
        "so", // type
        "so", // classifier
        null, // date

        // needs to be more general and not peer into the spec
        libraries.main.spec.outputFile,
        libraries.main.spec.task
    )

    task("assembleHeaders", type: Zip) {
        from libraries.main.headers
    }

    def headerArtifact = new DefaultPublishArtifact(
        "lib", // name
        "zip", // ext
        "zip", // type
        "headers", // classifier
        null, // date
        assembleHeaders.outputs.files.singleFile,
        assembleHeaders
    )

    artifacts {
        archives libArtifact, headerArtifact
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: uri("${buildDir}/repo"))
            }
        }
    }
}

project(":exe") {
    apply plugin: "cpp-exe"
    apply plugin: "eclipse-cdt"

    repositories {
        mavenRepo urls: "file://${new File(project(":lib").buildDir, "repo").absolutePath}"
    }

    cpp {
        sourceSets {
            main {
                dependency group: "dependencies", name: "lib", version: "1.0"
            }
        }
    }
}

task build(dependsOn: project(":exe").compileMain)