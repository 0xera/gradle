apply plugin: 'cpp'

cpp {
    sourceSets {
        exe {}
        lib {}
    }
}

libraries {
    // Define some common settings for all shared library binaries
    all {
        binaries.withType(SharedLibraryBinary) {
            define "DLL_EXPORT"
        }
    }
    helloEnglish {
        sourceSets << project.cpp.sourceSets.lib
    }
    helloFrench {
        sourceSets << project.cpp.sourceSets.lib
        binaries.all {
            define "FRENCH"
        }
    }
}

executables {
    english {
        sourceSets << project.cpp.sourceSets.exe
        binaries.all {
            // Wire the 'helloEnglish' shared library binary into the 'english' executable binary
            lib project.binaries.helloEnglishSharedLibrary
        }
    }
    french {
        sourceSets << project.cpp.sourceSets.exe
        binaries.all {
            // Wire the 'helloFrench' static library binary into the 'french' executable binary
            lib project.binaries.helloFrenchStaticLibrary
        }
    }
}
