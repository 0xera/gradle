// START SNIPPET create-uber-jar-example
plugins {
    id 'java'
}

version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.6'
}

task uberJar(type: Jar) {
    appendix = "uber"

    from sourceSets.main.output
    from configurations.runtimeClasspath.files.
                                         findAll { it.name.endsWith('jar') }.
                                         collect { zipTree(it) }
}
// END SNIPPET create-uber-jar-example

// START SNIPPET nested-specs
task nestedSpecs(type: Copy) {
    into "${buildDir}/explodedWar"
    exclude '**/*staging*'
    from('src/dist') {
        include '**/*.html', '**/*.png', '**/*.jpg'
    }
    from(sourceSets.main.output) {
        into 'WEB-INF/classes'
    }
    into('WEB-INF/lib') {
        from configurations.runtimeClasspath
    }
}
// END SNIPPET nested-specs
