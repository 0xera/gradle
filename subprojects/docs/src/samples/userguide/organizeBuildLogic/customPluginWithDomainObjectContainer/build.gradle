apply plugin: DocumentationPlugin

documentation{
    books {
        quickStart {
          sourceFile = file('src/docs/quick-start')
        }
        userGuide {

        }
        developerGuide {

        }
    }
}

task books << {
    documentation.books.each { book ->
        println "$book.name -> $book.sourceFile"
    }
}

class DocumentationPlugin implements Plugin<Project> {
    def void apply(Project project) {
        def books = project.container(Book)
        books.all {
            sourceFile = project.file("src/docs/$name")
        }
        project.extensions.documentation = new DocumentationPluginExtension(books)
    }
}

class Book {
    final String name
    File sourceFile

    Book(String name) {
        this.name = name
    }
}

class DocumentationPluginExtension{
    final NamedDomainObjectContainer<Book> books

    DocumentationPluginExtension(NamedDomainObjectContainer<Book> books) {
        this.books = books
    }

    def books(Closure cl) {
        books.configure(cl)
    }
}
