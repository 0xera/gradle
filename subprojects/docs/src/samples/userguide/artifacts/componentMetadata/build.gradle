import org.gradle.model.Mutate

repositories {
    ivy {
        url "$projectDir/repo"
    }
}

configurations {
    config1
    config2
    config3
    config4
}

// START SNIPPET latest-selector
dependencies {
    config1 "sea.fish:tuna:latest.integration"
    config2 "sea.fish:tuna:latest.release"
}

task listFish << {
    configurations.config1.each { println it.name }
    println()
    configurations.config2.each { println it.name}
}
// END SNIPPET latest-selector

// START SNIPPET custom-status-scheme
dependencies {
    config3 "air.birds:albatross:latest.silver"
    components {
        all { ComponentMetadataDetails details ->
            if (details.id.group == "air.birds") {
                details.statusScheme = ["bronze", "silver", "gold", "platinum"]
            }
        }
    }
}

task listAlbatross << {
    configurations.config3.each { println it.name }
}
// END SNIPPET custom-status-scheme

// START SNIPPET custom-status-scheme-module
dependencies {
    config4 "sea.fish:marlin:latest.rc"
    components {
        withModule('sea.fish:marlin') { ComponentMetadataDetails details ->
            details.statusScheme = ["int", "rc", "prod"]
        }
    }
}

task listMarlin << {
    configurations.config4.each { println it.name }
}
// END SNIPPET custom-status-scheme-module

// START SNIPPET ivy-component-metadata-rule
dependencies {
    components {
        all { ComponentMetadataDetails details, IvyModuleDescriptor ivyModule ->
            if (details.id.group == 'my.org' && ivyModule.branch == 'testing') {
                details.changing = true
            }
        }
    }
}
// END SNIPPET ivy-component-metadata-rule

// START SNIPPET rule-source-component-metadata-rule
dependencies {
    components {
        withModule('my.org:testing', new CustomStatusRule())
    }
}

class CustomStatusRule {
    @Mutate
    void setStatusScheme(ComponentMetadataDetails details) {
        details.statusScheme = ["bronze", "silver", "gold", "platinum"]
    }
}
// END SNIPPET rule-source-component-metadata-rule
