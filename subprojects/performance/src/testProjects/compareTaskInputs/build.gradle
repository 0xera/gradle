// use "-I init.gradle -u" cli parameters to run manually from build/compareTaskInputs directory

apply plugin:'base'

class OrdinaryTaskInputTask extends DefaultTask {
    @InputFiles
    FileCollection inputFiles

    @OutputFile
    File outputFile

    @TaskAction
    void run() {
        outputFile.text = name
    }
}

class IncrementalTaskInputTask extends DefaultTask {
    @InputFiles
    FileCollection inputFiles

    @OutputFile
    File outputFile

    @TaskAction
    void run(IncrementalTaskInputs inputs) {
        outputFile.text = name
    }
}

task buildIncremental

task buildOrdinary

def inputCount = project.hasProperty("inputCount") ? project.inputCount as int : 10000

def taskCount = project.hasProperty("taskCount") ? project.taskCount as int : 1000

for(int i=1;i <= taskCount;i++) {
    def taskIncremental=project.tasks.create(name: "incrementalTask$i", type: IncrementalTaskInputTask) {
        inputFiles = fileTree(dir:"src/${inputCount}files")
        outputFile = file("$buildDir/incrementalTask${i}_output")

    }
    buildIncremental.dependsOn taskIncremental

    def taskOrdinary=project.tasks.create(name: "ordinaryTask$i", type: OrdinaryTaskInputTask) {
        inputFiles = fileTree(dir:"src/${inputCount}files")
        outputFile = file("$buildDir/ordinaryTask${i}_output")
    }
    buildOrdinary.dependsOn taskOrdinary
}

if(project.hasProperty("changeOneInput")) {
    // modify last file in input directory
    File lastInputFile = file("src/${inputCount}files/file${inputCount}")
    assert lastInputFile.isFile()
    lastInputFile.text = "file changed ${System.currentTimeMillis()}"
}
