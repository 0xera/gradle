apply plugin: "groovy"

import org.gradle.build.*

configurations {
    mvn3Input
}

dependencies {
    groovy libraries.groovy

    compile project(":core")

    compile libraries.commons_httpclient
    compile libraries.commons_lang
    compile libraries.commons_io
    compile libraries.ivy
    compile libraries.slf4j_api
    compile libraries.maven_ant_tasks
    compile libraries.nekohtml

    testCompile libraries.junit

    compile fileTree("$buildDir/libs/jarjar") {
        builtBy 'jarJarMaven3'
    }

    mvn3Input libraries.maven3_settings_builder
}

task jarJarMaven3(type: JarJarAll) {
    inputJars = configurations.mvn3Input
    outputDir = file("$buildDir/libs/jarjar")

    //TODO SF
    //org.apache.maven -> org.gradle.mvn3.org.apache.maven
    rule('org.**', 'jarjar.org.@1')

    //TODO SF hide away or find a better way
    doLast {
        def plexusSec = "$outputDir/jarjar-plexus-sec-dispatcher-1.3.jar"
        def plexusSecNoComps = "$outputDir/jarjar-plexus-sec-dispatcher-1.3.jar.noComps"
        ant {
            zip(destfile: plexusSecNoComps, update: true) {
                zipfileset(src: plexusSec) {
                    exclude(name: 'META-INF/plexus/components.xml')
                }
            }
            move(file: plexusSecNoComps, tofile: plexusSec)
        }
    }
}


def allJarJars = tasks.withType(JarJarAll)
ideaModule.dependsOn allJarJars // I expected that buildable file collections links the ideaModule to the allJarJars already.
eclipseClasspath.dependsOn allJarJars
useTestFixtures()
classpathManifest.dependsOn(allJarJars) //TODO SF hotfix for the classpath manfiest issue, needs a better fix