apply plugin: 'groovy'

useTestFixtures()

sourceSets {
    smokeTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    smokeTestCompile.extendsFrom testCompile
    smokeTestRuntime.extendsFrom testRuntime
}

dependencies {
    smokeTestCompile project(":testKit")
    smokeTestCompile libraries.spock

    //so that implicit help tasks are available:
    smokeTestRuntime project(':diagnostics')
}

tasks.create('smokeTest', Test) { Test task ->
    dependsOn ':intTestImage' // for the Gradle user dir/Gradle Home dir
    shouldRunAfter 'test'

    testClassesDir = sourceSets.smokeTest.output.classesDir
    classpath = sourceSets.smokeTest.runtimeClasspath
    jvmArgs '-Xmx512m', '-XX:+HeapDumpOnOutOfMemoryError'
    if(!javaVersion.java8Compatible) {
        jvmArgs '-XX:MaxPermSize=768m'
    }

    reports.junitXml.destination = file("${project.testResultsDir}/$name")

    project.getTasksByName("configure${task.name.capitalize()}", false).first().doLast {
        configure(task) {
            reports.html.destination = file("${project.reporting.baseDir}/$name")
            systemProperties['smokeTest.gradleHomeDir'] = rootProject.intTestImage.destinationDir.absolutePath
            systemProperties['smokeTest.gradleUserHomeDir'] = rootProject.file('intTestHomeDir').absolutePath
        }
    }
}

plugins.withType(org.gradle.plugins.ide.idea.IdeaPlugin) { // lazy as plugin not applied yet
    idea {
        module {
            testSourceDirs += sourceSets.smokeTest.groovy.srcDirs
            testSourceDirs += sourceSets.smokeTest.resources.srcDirs
            scopes.TEST.plus.add(configurations.smokeTestCompile)
            scopes.TEST.plus.add(configurations.smokeTestRuntime)
        }
    }
}

plugins.withType(org.gradle.plugins.ide.eclipse.EclipsePlugin) { // lazy as plugin not applied yet
    eclipse {
        classpath {
            plusConfigurations.add(configurations.smokeTestCompile)
            plusConfigurations.add(configurations.smokeTestRuntime)
        }
    }
}
