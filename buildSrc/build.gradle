import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlinVersion = file('../kotlin-version.txt').text.trim()

    configure([repositories, project.repositories]) {
        maven { url 'https://repo.gradle.org/gradle/repo' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'

gradlePlugin {
    plugins {
        kotlinLibrary {
            id = "kotlin-library"
            implementationClass = "plugins.KotlinLibrary"
        }
        kotlinDslModule {
            id = "kotlin-dsl-module"
            implementationClass = "plugins.GskModule"
        }
        kotlinDslPublishedModule {
            id = "kotlin-dsl-published-module"
            implementationClass = "plugins.GskPublishedModule"
        }
    }
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = [
            "-Xjsr305=strict",
            "-Xskip-runtime-version-check"
        ]
    }
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile 'org.ow2.asm:asm-all:5.1'
    testCompile 'junit:junit:4.12'
    testCompile gradleTestKit()
}
