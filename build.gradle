buildscript {
    ext.kotlinVersion = '1.1.0-dev-218'
    repositories {
        maven {
            url 'https://repo.gradle.org/gradle/fram'
            credentials {
                username = artifactory_user
                password = artifactory_password
            }
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1'
    }
}

wrapper.gradleVersion = '2.13'

ext.kotlinVersion = '1.1.0-dev-218'

apply plugin: 'kotlin'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

group = 'org.gradle'
version = '1.0.0-SNAPSHOT'

dependencies {
    compileOnly 'org.gradle:gradle-core:2.14+'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
}


// --- classpath.properties --------------------------------------------
File generatedResourcesDir = file("$buildDir/generate-resources/main")
task generateClasspathManifest(type: codegen.GenerateClasspathManifest) {
    outputDirectory = generatedResourcesDir
}
sourceSets {
    main.output.dir generatedResourcesDir, builtBy: generateClasspathManifest
}


// -- Script config ----------------------------------------------------
configurations {
    kts
}
dependencies {
    kts gradleApi()
}
task generateKotlinScriptConfig(type: codegen.GenerateKotlinScriptConfig) {
    template = file("gradle.ktscfg.xml.template")
    outputFile = file("gradle.ktscfg.xml")
    classpath = configurations.kts
}

task prepare {
    description "Ensures a decent IDE experience."
    dependsOn generateKotlinScriptConfig, compileKotlin
}


// --- Configure publications ------------------------------------------
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "https://repo.gradle.org/gradle"
    publish {
        repository {
            repoKey = 'fram-local'
            username = artifactory_user
            password = artifactory_password
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repoKey = 'fram'
        username = artifactory_user
        password = artifactory_password
    }
}
