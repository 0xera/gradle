buildscript {
    ext.kotlinVersion = '1.1.0-dev-372'
    repositories {
        maven {
            url 'https://repo.gradle.org/gradle/repo'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1'
    }
}

wrapper.gradleVersion = '2.13'

apply plugin: 'kotlin'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

group = 'org.gradle'
version = '1.0.0-SNAPSHOT'

dependencies {
    compileOnly 'org.gradle:gradle-core:3+'
    compileOnly 'org.gradle:gradle-process-services:3+'
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'javax.inject:javax.inject:1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
}

jar {
    from sourceSets.main.allSource
}


// --- classpath.properties --------------------------------------------
File generatedResourcesDir = file("$buildDir/generate-resources/main")
task generateClasspathManifest(type: codegen.GenerateClasspathManifest) {
    outputDirectory = generatedResourcesDir
}
sourceSets {
    main.output.dir generatedResourcesDir, builtBy: generateClasspathManifest
}


// --- Configure publications ------------------------------------------
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "https://repo.gradle.org/gradle"
    publish {
        repository {
            repoKey = project.version.endsWith('-SNAPSHOT') ?
                'libs-snapshots-local' :
                'libs-releases-local'
            username = project.findProperty("artifactory_user") ?: "nouser"
            password = project.findProperty("artifactory_password") ?: "nopass"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repoKey = 'repo'
    }
}
