usePlugin 'java'

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom integrationTestCompile, testRuntime
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.5'
    integrationTestCompile group: 'commons-collections', name: 'commons-collections', version: '3.2'
}

source {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath = source.main.classes + source.test.classes + configurations.integrationTestCompile
        runtimeClasspath = classes + compileClasspath + configurations.integrationTestRuntime
    }
}

task integrationTest(type: Test, dependsOn: jar) {
    testClassesDir = source.integrationTest.classesDir
    configuration = source.integrationTest.runtimeClasspath
    options.systemProperties['jar.path'] = jar.archivePath
}

build.dependsOn integrationTest
